title ~~ PWARE SKEG v1.0 ~~
author Brian Stottler       â– 
homepage www.puzzlescript.net

text_color red
background_color white

noaction
noundo

========
OBJECTS
========

Background 
Gray

Wall 
White

One
Red
.....
.....
..0..
.....
.....

Two
Red DarkRed
.....
.000.
.010.
.000.
.....

Three
Red DarkRed
00000
01110
01110
01110
00000

Dead
Red
.0...
...00
.0.0.
0....
..00.

(Original gates:
OneGate
Gray DarkGray White LightBlue
22022
20002
00300
20002
22022

TwoGate
Gray DarkGray White Blue
20102
00030
10001
03000
20102

ThreeGate
Gray DarkGray White Purple
01110
10031
10301
13001
01110
)

OneGate
Gray Black
00000
00000
00100
00000
00000

TwoGate
Gray Black
00000
01110
01010
01110
00000

ThreeGate
Gray Black
11111
10001
10001
10001
11111

Plus
Orange
.....
..0..
.000.
..0..
.....

Minus
Yellow
.....
.....
.000.
.....
.....

Exit
LightGreen
..0..
.0.0.
0...0
.0.0.
..0..

=======
LEGEND
=======

. = Background
# = Wall
X = Exit

Player = One or Two or Three

1 = One
2 = Two
3 = Three

A = OneGate
B = TwoGate
C = ThreeGate

+ = Plus
- = Minus

NotOne = Two or Three
NotTwo = One or Three
NotThree = One or Two

=======
SOUNDS
=======

sfx0 41614303 (Death)
sfx1 71003703 (Grow)
sfx2 12761303 (Shrink)

================
COLLISIONLAYERS
================

Background
OneGate, TwoGate, ThreeGate, Plus, Minus, Exit
Player, Wall, Dead

======
RULES     
======     

(Gate size rules)
[> NotOne|OneGate] -> [NotOne|OneGate]
[> NotTwo|TwoGate] -> [NotTwo|TwoGate]
[> NotThree|ThreeGate] -> [NotThree|ThreeGate]

(Grow rules)
late [One Plus] -> [Two] sfx1
late [Two Plus] -> [Three] sfx1
late [Three Plus] -> [Dead] sfx0

(Shrink rules)
late [Three Minus] -> [Two] sfx2
late [Two Minus] -> [One] sfx2
late [One Minus] -> [Dead] sfx0

==============
WINCONDITIONS
==============

Some Player (the player must remain alive)
All Player on Exit

=======     
LEVELS
=======

(Level for debugging:
#########
#..A..++#
#..#..++#
#2.B...X#
#..#..--#
#..C..--#
#########
)

Message Nobody likes tutorials. Figure it out - I'm sure you're smart.

##############
#1..++-+--..X#
##############

Message See? Clever as a coconut.

########
#..++..#
#.####.#
#2####X#
#.####.#
#..--..#
########

Message Incredible.

##########
#.A.#.C..#
#.#.#.#..#
#1B.+.A.X#
#.#.#.#..#
#.C.#.B..#
##########

Message You can't possibly get this next one.

#########
#C...-#+#
#.#.###.#
#.#2A...#
#.#.#.#.#
#X#.C.#+#
#########

Message Truly brilliant.

Message That's it for now. Maybe I'll make more someday. I might even delete the childish commentary.